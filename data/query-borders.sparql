SELECT
	?country
	?countryLabel
	(GROUP_CONCAT(DISTINCT ?borderCountryLabel; separator="$DIVIDE$") AS ?borderCountries)
WHERE {
	SERVICE wikibase:label {
		bd:serviceParam wikibase:language "en,mul".
		?country rdfs:label ?countryLabel.
		?borderCountry rdfs:label ?borderCountryLabel.
	}.

	{
		?country wdt:P31 wd:Q3624078. # sovereign states as listed by Wikidata
		FILTER(?country != wd:Q756617) # not the whole Kingdom of Denmark
	} UNION {
		?country wdt:P31 wd:Q66724388. # constituent countries of the Kingdom of Denmark
	} UNION {
		VALUES (?country) {
			# (partially) unrecognised states
			(wd:Q1246) # Kosovo
			(wd:Q34754) # Somaliland
			(wd:Q907112) # Transnistria
			(wd:Q2842000) # Ambazonia
			(wd:Q31354462) # Abkhazia
			(wd:Q23427) # South Ossetia
			(wd:Q23681) # Northern Cyprus
			(wd:Q40362) # Western Sahara
			# dependent countries of New Zealand
			(wd:Q34020) # Niue
			(wd:Q26988) # Cook Islands
		}
	}


	# no dissolved countries
	FILTER NOT EXISTS{ ?country wdt:P576 ?date }

	OPTIONAL {
		?border wdt:P31 wd:Q12413618; wdt:P1001 ?country, ?borderCountry.
		FILTER(?borderCountry != ?country).
		FILTER EXISTS { ?border wdt:P31 wd:Q15104814 } # only land borders
	}
}
GROUP BY ?country ?countryLabel
